apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

// This is the library version used when deploying the artifact
group = "de.schroepf"
version = "0.2.1"

def siteUrl = 'https://github.com/schroepf/TestLab/tree/master/android/android-xml-run-listener'
def gitUrl = 'https://github.com/schroepf/TestLab'


android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 4
        versionName version
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'de.schroepf:android-xml-run-listener'
                description = 'JUnit XML RunListener for android instrumentation tests'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The Unlicense'
                        url 'http://unlicense.org/'
                    }
                }
                developers {
                    developer {
                        id 'schroepf'
                        name 'Tobias Schr√∂pf'
                        email 'schroepf@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

// https://github.com/bintray/gradle-bintray-plugin
bintray {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = 'schroepf'
        name = 'android-xml-run-listener'
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        publish = true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile ("com.android.support.test:runner:1.0.1") {
        exclude module: 'support-annotations'
    }

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.7.19'
}
